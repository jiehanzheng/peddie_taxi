//= require jquery.hoverIntent

window.PeddieTaxi ||= {}


bootstrapMap = () ->
  console.log "Google Maps JavaScript API has finished loading.  Preparing map..."

  jqMap = $(".map")
  PeddieTaxi.jqMap = jqMap

  # controls: display/hide certain map controls
  if $(".map").data("disable-default-ui")
    disableDefaultUI = true
    panControl = $(".map").data("pan-control")
    zoomControl = $(".map").data("zoom-control")
    mapTypeControl = $(".map").data("maptype-control")
    scaleControl = $(".map").data("scale-control")
    streetViewControl = $(".map").data("streetview-control")
    overviewMapControl = $(".map").data("overviewmap-control")

  # default values
  disableDefaultUI ?= false
  streetViewControl ?= true
  overviewMapControl ?= false

  # obtain base coordinates from HTML attribute on .venue-list
  $(".map").data "base-lat-long", new google.maps.LatLng $(".map").data("base-latitude"), 
                                                         $(".map").data("base-longitude")

  mapOptions = {
    center: $(".map").data "base-lat-long"
    # TODO: implement automatic zooming based on most markers
    zoom: 13
    mapTypeId: google.maps.MapTypeId.ROADMAP
    disableDefaultUI: disableDefaultUI
    panControl: panControl
    zoomControl: zoomControl
    mapTypeControl: mapTypeControl
    scaleControl: scaleControl
    streetViewControl: streetViewControl
    overviewMapControl: overviewMapControl }
  
  map = new google.maps.Map $(".map").get(0), mapOptions
  PeddieTaxi.map = map
  
  # make a marker for base first
  $(".map").data "base-marker", new google.maps.Marker {
    position: $(".map").data("base-lat-long")
    map: map
    title: "Peddie"
    icon: new google.maps.MarkerImage( "<%= asset_path('visual-case.it/gmaps_casetta_yellow_a1624.png') %>",
                                       new google.maps.Size(32, 32),
                                       new google.maps.Point(0,0),    # origin
                                       new google.maps.Point(16,24) ) # anchor
    shadow: new google.maps.MarkerImage( "<%= asset_path('visual-case.it/gmaps_casettas.png') %>",
                                         new google.maps.Size(59, 32),
                                         new google.maps.Point(0,0),      # origin
                                         new google.maps.Point(16,27) ) } # anchor

  console.log "Done adding Base marker."

  PeddieTaxi.do_action 'map_instantiated'

  # venues#index
  locationListItemsSync jqMap, map

  # venues#new
  # venues#edit
  draggableMarker jqMap, map
  
  # venues#new
  # venues#edit
  removeParentUponClick jqMap, map

  # venues#new
  # venues#edit
  autocomplete jqMap, map

  # venues#show
  requestDirection jqMap, map


# expose bootstrapMap() to the window
# in order for Google to call us back when it's done loading
PeddieTaxi.bootstrapMap = bootstrapMap


draggableMarker = (jqMap, map) ->
  if $('.map').data 'enable-draggable-marker'

    # if synced field already has a value, drop the marker there
    if $(jqMap.data 'draggable-marker-sync-lat').val() and $(jqMap.data 'draggable-marker-sync-lon').val()
      try
        markerLagLng = new google.maps.LatLng $(jqMap.data 'draggable-marker-sync-lat').val(), 
                                              $(jqMap.data 'draggable-marker-sync-lon').val()
      catch error
        console.log "draggableMarker: #{error}"

    markerLagLng ||= new google.maps.LatLng jqMap.data("base-latitude") + 0.001, 
                                            jqMap.data("base-longitude") - 0.001

    marker = new google.maps.Marker {
      position: markerLagLng
      map: map
      title: jqMap.data 'draggable-marker-title'
      draggable: true
      animation: google.maps.Animation.DROP }

    google.maps.event.addListener marker, 'position_changed', ->
      $(jqMap.data 'draggable-marker-sync-lat').val marker.getPosition().lat()
      $(jqMap.data 'draggable-marker-sync-lon').val marker.getPosition().lng()

    # TODO: the other way around

    # following line causes Uncaught RangeError: Maximum call stack size exceeded.  Why?
    # fitMarkers map, [jqMap.data("base-marker").getPosition(), markerLagLng]
    jqMap.data 'draggable-marker-handle', marker


locationListItemsSync = (jqMap, map) ->
  geoDataSource = $(".map-data-source")

  geoDataSource.find(".venue-list_item").each ->
    console.log "Adding marker from HTML data..."

    # tranfer lat,long data to an object, for use with Google Maps JavaScript API
    $(this).data "lat-long", new google.maps.LatLng $(this).data("latitude"), 
                                                    $(this).data("longitude")

    # make marker
    $(this).data "marker", new google.maps.Marker {
      position: $(this).data("lat-long")
      map: map
      title: $(this).data("name")
      animation: google.maps.Animation.DROP }

    
    # marker bounce
    #
    # bind hover event to item, in order to display relevant information 
    # when item is being hovered
    $(this).hoverIntent(
      ->
        console.log "marker bounce: hover"
        $(this).data("marker").setAnimation google.maps.Animation.BOUNCE

      ->
        console.log "marker bounce: leave"
        $(this).data("marker").setAnimation null
    )


removeParentUponClick = (jqMap, map) ->
  # if a user clicks on a link, it means that he is aware of the help-block,
  # then remove it to save space
  $(".remove-parent-upon-click").click ->
    $(this).parent().slideUp("slow")


autocomplete = (jqMap, map) ->
  autocompleteBounds = new google.maps.LatLngBounds(
    new google.maps.LatLng( <%= Settings.geo.boundaries.autocomplete.latlon1.latitude %>,
                            <%= Settings.geo.boundaries.autocomplete.latlon1.longitude %> ),
    new google.maps.LatLng( <%= Settings.geo.boundaries.autocomplete.latlon2.latitude %>,
                            <%= Settings.geo.boundaries.autocomplete.latlon2.longitude %> ) )

  $("#google_autocomplete").each -> 
    autocomplete = new google.maps.places.Autocomplete this, {
      bounds: autocompleteBounds
      componentRestrictions: {country: 'us'}
      types: [ $(this).data 'autocomplete-type' ] }

    google.maps.event.addListener autocomplete, 'place_changed', =>
      place = autocomplete.getPlace()

      # marker and viewport
      jqMap.data('draggable-marker-handle').setPosition(place.geometry.location)
      fitMarkers map, [jqMap.data("base-marker").getPosition(), place.geometry.location]

      # basic info
      $($(this).data 'autocomplete-output-name').val place.name
      $($(this).data 'autocomplete-output-formatted-address').val place.formatted_address
      $($(this).data 'autocomplete-output-vicinity').val place.vicinity
      $($(this).data 'autocomplete-output-website').val place.website
      $($(this).data 'autocomplete-output-reference').val place.reference


requestDirection = (jqMap, map) ->
  if jqMap.data("enable-direction")
    directionDestinationSelector = jqMap.data 'destination-selector'

    directionsService = new google.maps.DirectionsService()
    directionsDisplay = new google.maps.DirectionsRenderer()

    # listen to click event on the radio buttons
    $(jqMap.data 'travel-mode-selector').click ->
      travelMode = $(this).val()
      $(this).addClass "active"
      $(this).siblings().removeClass "active"

      if travelMode
        $(jqMap.data 'hide-once-travel-mode-is-selected').slideUp 'slow'

        directionsDisplay.setMap map

        if jqMap.data 'enable-direction-display'
          directionsDisplay.setPanel $("#directions_panel").get(0)

        directionsService.route {
          origin: jqMap.data "base-lat-long"
          destination: $(directionDestinationSelector).data "lat-long"
          travelMode: google.maps.TravelMode[travelMode] }, (response, status) ->
          if status is google.maps.DirectionsStatus.OK
            directionsDisplay.setDirections response
          else
            errorAlert = """
                         <div class="alert alert-error">
                         <button type="button" 
                                 class="close" 
                                 data-dismiss="alert">&times;</button>
                         <strong>Error:</strong> #{status}
                         """
          
            $(this).parent().prepend( errorAlert )
      else
        console.log "No travel mode was selected."


fitMarkers = (map, positions) ->
  bounds = new google.maps.LatLngBounds()
  bounds.extend position for position in positions
  map.fitBounds(bounds)


# load Google libs if it's not loaded already
if $(".map").length
  if (not window.google?) or (not google.maps?)
    jQuery.getScript "//maps.googleapis.com/maps/api/js?v=3.12&libraries=places&key=<%= Settings.google.api.simple.key %>&sensor=false&callback=PeddieTaxi.bootstrapMap", ->
      console.log "Google Maps JavaScript API is starting to load."
  else
    bootstrapMap()
